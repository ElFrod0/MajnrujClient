plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.github.juuxel.loom-quiltflower' version '1.8.+'
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'com.modrinth.minotaur' version '2.+'
    id 'me.hypherionmc.cursegradle' version '2.+'
    id 'com.github.breadmoirai.github-release' version '2.+'
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
project.build_num = System.getenv("GITHUB_RUN_NUMBER") ?: "SNAPSHOT"
version = project.minecraft_version + "-" + project.build_num
group = project.maven_group

repositories {
    mavenLocal()
    maven {
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        url = "https://maven.shedaniel.me"
    }
    maven {
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { 
        url = "https://jitpack.io"
    }
    maven {
        url = 'https://maven.parchmentmc.org'
    }
    mavenCentral()
}

dependencies {
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${project.parchment_version}@zip")
    }
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

    // this is dumb as hell. bloats jar way too much. todo: switch to a different config provider
    include(modImplementation("org.spongepowered:configurate-hocon:${project.configurate_version}"))
    include("org.spongepowered:configurate-core:${project.configurate_version}")
    include("com.google.guava:guava:31.1-jre")
    include("com.typesafe:config:1.4.2")
    include("io.leangen.geantyref:geantyref:1.3.12")
    implementation ("com.github.oshi:oshi-core:6.4.5")

    // compatibility
    modImplementation("com.terraformersmc:modmenu:6.2.0")
    modCompileOnly("curse.maven:minimal-menu-405795:3826009")
    modImplementation("curse.maven:minimal-menu-405795:3826009")
    //modRuntimeOnly("net.fabricmc.fabric-api:fabric-api:0.78.0+1.19.4")

    // Discord Rich Presence
    include(implementation("com.github.jncrmx:discord-game-sdk4j:0.5.5")) // This is from mavenLocal() -> https://github.com/JnCrMx/discord-game-sdk4j | Perhaps I should include this as a part of the repo.
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.properties["minecraft_version"]
    inputs.property "loader_version", project.properties["loader_version"]

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(21)
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

loom {
    mixin.defaultRefmapName = "majnrujclient.refmap.json"
    accessWidenerPath = file("src/main/resources/majnrujclient.accesswidener")
}

modrinth {
    //token = System.getenv("MODRINTH_TOKEN")
    projectId = "majnrujclient"
    versionName = project.minecraft_version + " build " + project.build_num
    versionNumber = project.version
    versionType = "release"
    uploadFile = remapJar
    gameVersions = [project.minecraft_version]
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
    }
    //changelog = System.getenv("COMMIT_MESSAGE")
}
